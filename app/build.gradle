plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jlleitschuh.gradle.ktlint' version '11.0.0' //  ktLint
}

apply from: '../app-version.gradle'

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.akundu.kkplayer"
        minSdk 23
        targetSdk 33
        versionCode project.ext.version.major * 100 + project.ext.version.minor * 10 + project.ext.version.build
        versionName "${project.ext.version.major}.${project.ext.version.minor}.${project.ext.version.build}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        resConfigs "en", "en-rGB", "en-rIN"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.10'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

// Automatically format code using ktLint before run the project
tasks.findByPath("preBuild").dependsOn("ktlintFormat")

// ktLint (https://www.youtube.com/watch?v=hSgPNbEcX98)
ktlint {
    android = true
    ignoreFailures = false
    disabledRules = ["max-line-length","parameter-list-wrapping", "no-multi-spaces",
                     "no-consecutive-blank-lines", "no-blank-line-before-rbrace", "final-newline"]
    reporters {
        reporter 'plain'
        reporter 'checkstyle'
        reporter 'sarif'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.compose.material3:material3:1.0.0-alpha11'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    //implementation "io.coil-kt:coil-compose:2.1.0"                            // to use shape drawable in compose

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.0'

    /** Work */
    implementation 'androidx.work:work-runtime-ktx:2.8.1'

    /** Retrofit */
    def retrofit_version = '2.5.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.2"
    implementation "com.squareup.okhttp3:okhttp:4.2.2"
    implementation 'com.squareup.picasso:picasso:2.71828'


    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    /** Test */
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:0.40"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'

    // SplashScreen dependency in Android 12
    implementation 'androidx.core:core-splashscreen:1.0.0'

    implementation 'com.github.GrenderG:Toasty:1.5.2'
}
